plugins {
    id 'java'
}

group 'org.iota'
version '0.2.1'

sourceCompatibility = 1.7

repositories {
    maven { url "https://jitpack.io" }
    mavenCentral()
}

dependencies {
    compile group: 'junit', name: 'junit', version: '4.12'

    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.11.1'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.1'

    // used until pull request for iotaledger/iota.curl.java is merged
    compile 'com.github.mikrohash:iota.curl.java:master'
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Main-Class': 'org.iota.ict.Main'
    }
    baseName = 'ict'
    destinationDir = file("$rootDir/")
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task runDocGen(type:Exec) {
    // gen_classes.py cannot be included in this repository because of license issues.
    workingDir("$rootDir/docs")
    commandLine 'python', 'gen.py'
}

task validateRelease() {
    dependsOn(runDocGen)
    doFirst {
        println "=== RELEASING VERSION " + version + " ==="
        assert !version.contains("SNAPSHOT")
        assert 'git status --porcelain -uno'.execute().text.length() == 0
    }
}

task release {
    dependsOn(validateRelease)
    dependsOn(test)
    finalizedBy(fatJar)
}